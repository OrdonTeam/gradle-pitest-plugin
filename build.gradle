plugins {
    id 'com.gradle.plugin-publish' version '0.9.4'
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'

group = 'pl.droidsonroids.gradle'

sourceCompatibility = 1.7

project.version = '0.0.2'

repositories {
    mavenCentral()
    mavenLocal()
}

sourceSets {
    funcTest {
        java.srcDir file('src/funcTest/java')
        groovy.srcDir file('src/funcTest/groovy')
        resources.srcDir file('src/funcTest/resources') //Broken in Idea - https://youtrack.jetbrains.com/issue/IDEA-128966
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:2.1.2'

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        //groovy 2.3.x is already provided by Gradle itself
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:2.2.2'   //for Spying in Spock
    testCompile 'junit:junit:4.12'

    funcTestCompile sourceSets.main.output
    funcTestCompile sourceSets.test.output  //to make visible funcTest resources moved to test resources as a workaround for Idea bug

    funcTestCompile configurations.testCompile
    funcTestRuntime configurations.testRuntime
    //dependencies from non default configurations are ignored in Idea - https://youtrack.jetbrains.com/issue/IDEA-121728
    /*funcT*/testCompile('com.netflix.nebula:nebula-test:4.0.0') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

task funcTest(type: Test) {
    testClassesDir = sourceSets.funcTest.output.classesDir
    classpath = sourceSets.funcTest.runtimeClasspath

    reports.html {
        destination = file("${reporting.baseDir}/funcTests")
    }
}
funcTest.shouldRunAfter test
check.shouldRunAfter funcTest

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    reportOn test, funcTest
}

publishPlugins.dependsOn funcTest, check

wrapper {
    gradleVersion "2.14"
}

pluginBundle {
    website = 'https://github.com/koral--/gradle-pitest-plugin'
    vcsUrl = 'https://github.com/koral--/gradle-pitest-plugin.git'
    tags = ['pitest', 'android', 'mutation testing']
    description = 'Gradle plugin for PIT Mutation Testing in Android projects'

    plugins {
        pitestPlugin {
            id = 'pl.droidsonroids.pitest'
            displayName = 'Android pitest plugin'
        }
    }
}

publishing {
    repositories {
        mavenLocal()
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}