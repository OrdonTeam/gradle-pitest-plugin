buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.0"
        classpath 'com.android.tools.build:gradle:2.2.3'
    }
}

subprojects {
    apply plugin: 'pl.droidsonroids.pitest'
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'

    android {
        buildToolsVersion '25.0.2'
        compileSdkVersion 25
        defaultConfig {
            minSdkVersion 10
            targetSdkVersion 25
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile 'junit:junit:4.12'
        compile "org.jetbrains.kotlin:kotlin-stdlib:1.1.0"
    }

    version = '1.0'
    group = 'pitest.sample.multimodule'
}

configure(project(":shared")) {
    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.21'
        compile 'org.slf4j:slf4j-nop:1.7.21'
    }
}

configure(project(':itest')) {
    apply plugin: 'pl.droidsonroids.pitest'
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'

    android {
        buildToolsVersion '25.0.2'
        compileSdkVersion 25
        defaultConfig {
            minSdkVersion 10
            targetSdkVersion 25
        }
    }
    dependencies {
        compile project(':shared')
    }

    //Additional configuration to resolve :shared project JAR as mutable code path for PIT
    configurations { mutableCodeBase { transitive false } }
    dependencies { mutableCodeBase project(':shared') }
    pitest {
        timestampedReports = false
        mainSourceSets = [project.android.sourceSets.main, project(':shared').android.sourceSets.main]
        additionalMutableCodePaths = [configurations.mutableCodeBase.singleFile]
//        additionalMutableCodePaths = project(':shared').jar.outputs.files.getFiles()  //Should also work
    }
}
